/*
 * Copyright (c) KindedJ 2017
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *    http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.7.3'
    }
}

allprojects {
    ext {
        baseVersion = '1.1.0'
        isSnapshot = false
    }
}

subprojects {

    apply plugin: 'java-library'

    sourceCompatibility = "1.8"
    targetCompatibility = '1.6'

    task javadocJar(type: Jar, dependsOn: "javadoc") {
        classifier = 'javadoc'
        from "build/docs/javadoc"
    }

    task sourcesJar(type: Jar) {
        from sourceSets.main.allSource
        classifier = 'sources'
    }

    artifacts {
        archives jar
        archives javadocJar
        archives sourcesJar
    }

    apply plugin: 'maven'

    group = 'io.kindedj'
    version = baseVersion + (isSnapshot ? "-SNAPSHOT" : "")

    repositories {
        jcenter()
    }

    install {
        repositories {
            mavenInstaller {
                pom {
                    project {
                        name 'KindedJ'
                        packaging 'jar'
                        description 'A shared interface for evidence-based Higher Kinded Types on the JVM.'
                        url 'https://github.com/KindedJ/KindedJ'
                        organization {
                            name 'KindedJ Team'
                        }
                        scm {
                            url 'https://github.com/KindedJ/KindedJ'
                        }
                        licenses {
                            license {
                                name 'The Apache Software License, Version 2.0'
                                url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                                distribution 'repo'
                            }
                        }
                        developers {
                            developer {
                                name 'Paco'
                            }
                            developer {
                                name 'Jean-Baptiste Giraudeau'
                            }
                        }
                    }
                }
            }
        }
    }

    apply plugin: 'com.jfrog.bintray'
    bintray {
        user = project.hasProperty('bintrayUser') ? project.property('bintrayUser') : System.getenv('BINTRAY_USER')
        key = project.hasProperty('bintrayApiKey') ? project.property('bintrayApiKey') : System.getenv('BINTRAY_API_KEY')
        configurations = ['archives']
        pkg {
            repo = 'maven'
            name = 'kindedj'
            userOrg = 'kindedj'
        }
    }
}

task wrapper(type: Wrapper) {
    distributionUrl = "https://services.gradle.org/distributions/gradle-${gradleVersion}-all.zip"
}


task clean(type: Delete) {
    delete rootProject.buildDir
}
